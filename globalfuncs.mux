# Global functions.
# These should all be added with @function/preserve afterwards.
# GFO = Global Function Object, change as needed.
#
# HEADER | FOOTER | LINE | SUBHEADER | SUBFOOTER | SUBLINE
# Arguments for headers/footers:
# %0 - text
# %1 - length, defaults to 78
# Arguments for lines:
# %0 - length, defaults to 78
# Registers:
# %ql - length
&HEADER GFO=
    [setq(l,if(%1,%1,78))]
    %xh%xy/
    %xb[repeat(=,5)]
    [ljust(
        if(%0,%xy/%xw [left(%0,sub(%ql,18))] %xy/)
    ,sub(%ql,7),%xb=)]
    %xh%xy/
-
&FOOTER GFO=
    [setq(l,if(%1,%1,78))]
    %xh%xy/
    [rjust(
        if(%0,%xy/%xw [left(%0,sub(%ql,18))] %xy/)
    ,sub(%ql,7),%xb=)]
    %xh%xb[repeat(=,5)]
    %xy/
-
&LINE GFO=
    [setq(l,if(%0,%0,78))]
    %xh%xy/
    %xh%xb[repeat(=,sub(%ql,2))]
    %xy/
-
&SUBHEADER GFO=
    [setq(l,if(%1,%1,78))]
    %xh%xy/
    %xb[repeat(-,5)]
    [ljust(
        if(%0,%xy<%xw [left(%0,sub(%ql,18))] %xy>)
    ,sub(%ql,7),%xb-)]
    %xh%xy/
-
&SUBFOOTER GFO=
    [setq(l,if(%1,%1,78))]
    %xh%xy/
    [rjust(
        if(%0,%xy<%xw [left(%0,sub(%ql,18))] %xy>)
    ,sub(%ql,7),%xb-)]
    %xh%xb[repeat(-,5)]
    %xy/
-
&DIVIDER GFO=
    [setq(l,if(%0,%0,78))]
    %xh%xy/
    %xh%xb[repeat(-,sub(%ql,2))]
    %xy/
-
&GAMEPREF GFO=
	GAME:%b
-
&GETWIDTH GFO=
	if(hasflag(%0,nowidth),78,width(%0))
-
&UNIQUE GFO=
	setunion(%0,)
-
# list, field width, line length, indent, delimiter
# %0 - list
# %1 - field width
# %2 - line length
# %3 - indent
# %4 - delimiter
&VARCOLUMN GFO=
	[trim(trim([repeat(%b,%3)]
	[setq(l,sub(%2,%3))]
	[setq(0,0)]
	[edit(iter(%0,
		[ljust(##,sub(%1,1))]%b
		[if(
			[setq(0,add(%q0,%1))][gt(%q0,sub(%ql,%1))],
			%R[repeat(%b,%3)][setq(0,0)],
			
		)]
	,%4,~~FIX~ITER~DELIM~~),~~FIX~ITER~DELIM~~,)],r,%b),r,%r)]
-
&ISSTAFF GFO=
	orflags(%0,WwZ)
-
&PLAYERID GFO=
	[setr(0,pmatch(%0))]:[convtime(ctime(%0))]
-
&IDPLAYER GFO=
	[first(%0,:)]
-
# %0:  List of data.
# %1:  List of widths.
# %2:  Input delimiter.
# %3:  Output delimiter.
&FIELDFMT GFO=
	[iter(
		lnum(1,words(%0,%2)),
		[ljust(
			extract(%0,##,1,%2),
			extract(%1,##,1)	
		)]
	
	)]
	
-
&PARSEREG GFO=
	iter(lnum(1,words(%0,%2)),
		setq(
			extract(%1,##,1),
			extract(%0,##,1,%2)
		)
	,,@@)
-
&PLURALIZE GFO=
	[if(strmatch(%0,1),
		%0 %1,
		%0 %2)]
-
&SQLESCAPE GFO=
	edit(edit(%0,',\\'),\,,\\\,)
-
@dolist PARSEREG=@function ##=GFO/##
-
@dolist DIVIDER FOOTER GAMEPREF HEADER LINE SUBFOOTER SUBHEADER UNIQUE VARCOLUMN ISSTAFF PLAYERID IDPLAYER FIELDFMT PLURALIZE SQLESCAPE=@function/preserve ##=GFO/##
-
@dolist GETWIDTH=@function/privilege/preserve ##=GFO/##
-